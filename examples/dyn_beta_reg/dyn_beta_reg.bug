# Dynamic beta regression
# cf Da Silva et al. (2009)
# Bayesian beta dynamic model and applications. Working paper,
# Department of Statistics, Federal University of Rio de Janeiro.
# 
# Model to analyze the Brazilian monthly unemployment rate

var x.true[t.max], x[t.max], mu.true[t.max],mu[t.max], y[t.max]

data
{
  x.true[1] ~ dnorm(mean.x.init, prec.x.init)
  for (t in 2:t.max)
  { 
    x.true[t] ~ dnorm(x.true[t-1], prec.x)
    mu.true[t] <- 1/(1+exp(-x.true[t]))
    # logit(mu.true[t]) <- x.true[t]    
    # mu.true[t] <- ilogit(x.true[t])
    y[t] ~ dbeta(phi*mu.true[t], phi*(1-mu.true[t]))
  }
}

model
{
  x[1] ~  dnorm(mean.x.init, prec.x.init)
  for (t in 2:t.max)
  { 
    x[t] ~ dnorm(x[t-1], prec.x)
    mu[t] <- 1/(1+exp(-x[t]))
    # logit(mu[t]) <- x[t]    
    # mu.true[t] <- ilogit(x.true[t])
    y[t] ~ dbeta(phi*mu[t], phi*(1-mu[t]))
  }
}
