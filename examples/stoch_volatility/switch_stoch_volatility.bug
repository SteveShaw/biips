# Switching Stochastic Volatility Model
# Reference: C.M. Carvalho and H.F. Lopes. Computational Statistics and Data analysis (2007) 4526-4542.

var y[t_max,1], x[t_max,1], prec_y[t_max,1],mu[t_max,1],mu_true[t_max,1],alpha[2],c[t_max],c_true[t_max],alp[2,1]

data
{  
  x_true[1,1] ~ dnorm(0, 1/sigma_true^2)
  c_true[1] <- 1;  
  prec_y_true[1,1] <- exp(-x_true[1,1]) 
  y[1,1] ~ dnorm(0, prec_y_true[1,1])
  for (t in 2:t_max)
  { 
    c_true[t] ~ dcat(ifelse(c_true[t-1]==1,pi[1,],pi[2,]))
    mu_true[t,1] <- alp_true[1,1]*(c_true[t]==1) + alp_true[2,1]*(c_true[t]==2) + beta_true*x_true[t-1,1];
    x_true[t,1] ~ dnorm(mu_true[t,1], 1/sigma_true^2)
    prec_y_true[t,1] <- exp(-x_true[t,1])  
    y[t,1] ~ dnorm(0, prec_y_true[t,1])
  }
}

model
{
  alp[1,1] ~ dnorm(0,10000)
  alp[2,1] ~ dnorm(0,10000)
  logit_beta ~ dnorm(0,.1)
  beta <- 1/(1+exp(-logit_beta))
  log_sigma ~ dnorm(0, 1)
  sigma <- exp(log_sigma)
  
  x[1,1] ~ dnorm(0, 1/sigma^2)
  c[1] <- 1;  
  prec_y[1,1] <- exp(-x[1,1]) 
  y[1,1] ~ dnorm(0, prec_y[1,1])
  for (t in 2:t_max)
  { 
    c[t] ~ dcat(ifelse(c[t-1]==1, pi[1,], pi[2,]))
    mu[t,1] <- alp[1,1] * (c[t]==1) + alp[2,1]*(c[t]==2) + beta*x[t-1,1];
    x[t,1] ~ dnorm(mu[t,1], 1/sigma^2)
    prec_y[t,1] <- exp(-x[t,1])  
    y[t,1] ~ dnorm(0, prec_y[t,1])
  }
}